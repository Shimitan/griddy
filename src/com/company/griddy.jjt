options {
    OUTPUT_DIRECTORY = "src/com/company";
    MULTI = true;
    NODE_PACKAGE = "com.company.parser";
    JJTREE_OUTPUT_DIRECTORY = "src/com/company/parser";
    VISITOR = true;
    LOOKAHEAD = 1;
    NODE_DEFAULT_VOID = true;
}

PARSER_BEGIN(Griddy)
package com.company;

import java.util.ArrayList;

public class Griddy {
    public static void main(String args[]) {
        Griddy parser = new Griddy(System.in);
        StringBuilder output = new StringBuilder();

        try {
            ASTStart n = parser.Start();
            if (args.length >0 && args[0].equals("t")) n.dump("");
            else {
                Visitor v = new Visitor();
                n.jjtAccept(v, output);
                System.out.println(output);
            }
        } catch (Exception e) {
            System.out.println("An error occurred.");
            System.out.println(e.getMessage());
            e.printStackTrace();
        }
    }
}

PARSER_END(Griddy)

/* Whitespace characters */
SKIP : {
        < WHITE_SPACE:  <TAB> | <NEWLINE> | <SP>    >
    |   < #SP:   " "     >
    |   < #TAB:  "\t"    >
}

/* Comments */
SPECIAL_TOKEN : {
        <SINGLE_LINE_COMMENT:   "#" (~["\n","\r"])* <NEWLINE>   >
    |   < NEWLINE:  "\n" | "\r" | "\r\n"    >   // line endings
}

/* Reserved keywords */
TOKEN : {
        < IF:   "if"    >   // if statement
    |   < ELSE: "else"  >   // else statement
    |   < FROM: "from"  >   // get value 'from' board, e.g. '(x,y) from my_board'
    |   < ON:   "on"    >   // value exist on board, e.g. 'game_piece on my_board'
    |   < IN:   "in"    >   // e.g. 'for x in iterable'
    |   < FOR:  "for"   >   // for loop begin
    |   < END:  "end"   >   // block end
    |   < ECHO: "echo"  >   // prints the output
    |   < GAME_SETUP:   "SETUP" >   // setup phase begin
    |   < GAME_MAIN:    "GAME"  >   // game phase begin
    |   < FUNC_DECL:    "fun"   >   // function declaration
    |   < BOARD_DECL:   "board" >   // board declaration token, e.g. 'board (n,m)'
}

/* Punctuators */
<DEFAULT, IN_REGEX>
TOKEN : {
        < EQ:   "=="    > : DEFAULT // equal
    |   < NEQ:  "!="    > : DEFAULT // 'not equal'
    |   < GEQ:  ">="    > : DEFAULT // 'greater than or equal to'
    |   < LEQ:  "<="    > : DEFAULT // 'less than or equal to'
    |   < NOT:  "!"     > : DEFAULT // 'not' / 'negate'
    |   < LT:   "<"     > : DEFAULT // 'less than'
    |   < GT:   ">"     > : DEFAULT // 'greater than'
    |   < AND:  "&&"    > : DEFAULT // logical 'and'
    |   < OR:   "||"    > : DEFAULT // logical 'or'
    |   < PLUS: "+"     > : DEFAULT // addition
    |   < MINUS:"-"     > : DEFAULT // subtraction
    |   < MULT: "*"     > : DEFAULT // multiplication
    |   < DIV:  "/"     > : DEFAULT // division
    |   < MOD:  "%"     > : DEFAULT // modulus
    |   < INCR:  "++"   > : DEFAULT // increment
    |   < DECR:  "--"   > : DEFAULT // decrement
    |   < ASSIGN: "="   > : DEFAULT // variable assigment
    |   < PLUS_ASSIGN:  "+="    > : DEFAULT // add and assign
    |   < MINUS_ASSIGN: "-="    > : DEFAULT // subtract and assign
    |   < MULT_ASSIGN:  "*="    > : DEFAULT // multiply and assign
    |   < DIV_ASSIGN:   "/="    > : DEFAULT // divide and assign
    |   < DOT:      "." > : DEFAULT     // dot (struct property access maybe?)
    |   < COMMA:    "," > : DEFAULT     // seperator comma
    |   < SEMICOLON: ";"> : DEFAULT     // statement endings
    |   < LPAREN:   "(" > : DEFAULT     // left parenthesis
    |   < RPAREN:   ")" > : IN_REGEX    // right parenthesis
    |   < LBRACE:   "{" > : DEFAULT     // left curly bracket
    |   < RBRACE:   "}" > : IN_REGEX    // right curly bracket
    |   < LBRACKET: "[" > : DEFAULT     // left square bracket
    |   < RBRACKET: "]" > : IN_REGEX    // right square bracket
}

/* Literals (terminals) */
<DEFAULT, IN_REGEX>
TOKEN : {
        < BOOL: "true" | "false"    > : IN_REGEX
    |   < NULL: "empty" > : DEFAULT
    |   < NUM:  ["0"-"9"] | ["1"-"9"] (["0"-"9"])+  > : DEFAULT
    |   < NEG_NUM:  "-" <NUM>   > : DEFAULT
    |   < STRING:  "\"" (~["\"","\r","\n"])* "\"" > : DEFAULT // string literal
}

/* Identifier (terminals) */
TOKEN : {
        < IDENT:        <IDENT_PART> (<IDENT_PART>)* >
    |   < #IDENT_PART:  (<LETTER> | "_")+ >
    |   < #LETTER:      ["a"-"z","A"-"Z"] >
}

ASTStart Start() #Start : {}
{
    setupPhase()
    gamePhase()
    <EOF>
    { return jjtThis; }
}

void setupPhase() #Setup : {}
{
    <GAME_SETUP>
    (setupStmt())*
}

void setupStmt() : {}
{
        assignment() <SEMICOLON>
}

void gamePhase() #Game : {}
{
    <GAME_MAIN>
    (gameStmt())+
}

void gameStmt() : {}
{
        assignment() <SEMICOLON>
    |   echoStmt() <SEMICOLON>

}

void boardDeclaration() #Board :
{
    Token a, b;
    ArrayList<Integer> boardSize = new ArrayList<Integer>();
}
{
    <BOARD_DECL> <LPAREN> a=<NUM> <COMMA> b=<NUM> <RPAREN>
    {
        boardSize.add(Integer.parseInt(a.image));
        boardSize.add(Integer.parseInt(b.image));
        jjtThis.jjtSetValue(boardSize);
    }
}

void assignment() #Assign : {}
{
    identifier() <ASSIGN> (boardDeclaration() | additiveExpr() | stringLiteral())
}


void additiveExpr() #Expr(>1) : {}
{
    multiplicativeExpr() ( additiveOperator() multiplicativeExpr() )*
}

ASTOperator additiveOperator() #Operator :
{
    Token t;
}
{
    (t=<PLUS> | t=<MINUS>)
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

void multiplicativeExpr() #Expr(>1) : {}
{
    unary() ( multiplicativeOperator() unary() )*
}

ASTOperator multiplicativeOperator() #Operator :
{
    Token t;
}
{
    (t=<MULT> | t=<DIV> | t=<MOD>)
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

void unary() : {}
{
        <LPAREN> additiveExpr() <RPAREN>
    |   primary()
    |   identifier()
}

void primary() : {}
{
        number()
    |   bool()
}

ASTIdent identifier() #Ident :
{
    Token t;
}
{
    t=<IDENT>
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

ASTInteger number() #Integer :
{
    Token t;
}
{
    t=<NUM>
    {
        jjtThis.jjtSetValue(Integer.parseInt(t.image));
        return jjtThis;
    }
}

ASTBool bool() #Bool :
{
    Token t;
}
{
    t=<BOOL>
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

ASTString stringLiteral() #String :
{
    Token t;
}
{
    t=<STRING>
    {
        jjtThis.jjtSetValue(t.image.substring(1, (t.image.length() - 1)));
        return jjtThis;
    }
}

ASTEcho echoStmt() #Echo : {}
{
    <ECHO> ( additiveExpr() | stringLiteral() )
    {
        return jjtThis;
    }
}